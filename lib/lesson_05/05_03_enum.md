# Модуль Enum

[Модуль Enum](https://hexdocs.pm/elixir/Enum.html) -- основной способ работы с коллекциями. Он содержит уже известные нам функции map, filter, reduce, и еще несколько десятков других функций.

Рассмотрим некоторые из них. Но прежде разберемся, что такое "коллекции".

Коллекциями являются не только списки, но и многие другие типы данных: String, Map, MapSet, Range. Они по-разному устроены, но предоставляют общий АПИ, благодаря которому модуль Enum может работать со всеми ними одинаково.

Этот АПИ называется протокол Enumerable. Протоколы мы будем рассматривать позже. Сейчас важно знать, что для разных структуры данных можно реализовать одинаковый протокол, что дает нам полиморфизм. Это похоже на то, как работают интерфейсы в Java.


## sort

Начнем с сортировки. В BEAM любые значения можно сравнивать между собой, даже значения разных типов. Сравнение внутри одного типа удивления не вызывает, но как сравнить атом и кортеж? В BEAM принята некоторая иерархия типов, в рамках которой считается, что любой атом больше любого числа. В целом это выглядит так:

number < atom < reference < function < port < pid < list < tuple < map < bitstring

Так что мы можем сортировать что угодно:
```
iex(1)> Enum.sort([1,31,5,3.14,6])
[1, 3.14, 5, 6, 31]
iex(2)> Enum.sort([:hello, 42, {1, 2}, "world", fn x -> x end, %{:a => 42}, 555])
[
  42,
  555,
  :hello,
  #Function<44.97283095/1 in :erl_eval.expr/5>,
  {1, 2},
  %{a: 42},
  "world"
]
```

Однако, чаще нам нужно сравнивать пользовательские типы данных, для которых нужно указать свои правила сортировки. Например, список пользователей мы хотели бы сортировать по имени. Для этого в Enum.sort нужно передать свою функцию, которая получает два элемента из списка, и сообщает, какой из них больше. Функция должна вернуть true, если первый элемент меньше или равен второму, и false, если первый элемент больше второго:

```
iex(2)> users = HOF.test_data
[
  {:user, 1, "Bob", 23},
  {:user, 2, "Helen", 20},
  {:user, 3, "Bill", 15},
  {:user, 4, "Kate", 14}
]
iex(11)> sorter = fn({:user, _, name1, _}, {:user, _, name2, _}) -> name1 <= name2 end
#Function<43.97283095/2 in :erl_eval.expr/5>
iex(12)> Enum.sort(users, sorter)                                                     
[
  {:user, 3, "Bill", 15},
  {:user, 1, "Bob", 23},
  {:user, 2, "Helen", 20},
  {:user, 4, "Kate", 14}
]
```


## zip

В функциональном программировании не редко встречается задача, когда нужно взять два списка, и как-то объединить их элементы попарно. Это делает функция zip. В простом варианте она просто соединяет элементы в кортеж:

```
iex(14)> Enum.zip([1,2,3,4], [:a, :b, :c, :d])
[{1, :a}, {2, :b}, {3, :c}, {4, :d}]
```

Списки могут быть разной длины, тогда Enum.zip завершится вместе с коротким списком:
```
iex(20)> Enum.zip([1,2], [:a, :b, :c, :d])
[{1, :a}, {2, :b}]
iex(21)> Enum.zip([1,2,3,4], [:a, :b, :c])    
[{1, :a}, {2, :b}, {3, :c}]
```

Иногда мы хотим не просто объединить два элемента в кортеж, но как-то вычислить новый элемент на основе первых двух. В Эрланг для этого есть функция :lists:zipwith/2. В Эликсир такой функции нет, но можно решить эту задачу в два прохода -- zip и map:
```
iex(24)> users
[
  {:user, 1, "Bob", 23},
  {:user, 2, "Helen", 20},
  {:user, 3, "Bill", 15},
  {:user, 4, "Kate", 14}
]
iex(25)> Enum.zip([10, 20, 30, 40], users) 
[
  {10, {:user, 1, "Bob", 23}},
  {20, {:user, 2, "Helen", 20}},
  {30, {:user, 3, "Bill", 15}},
  {40, {:user, 4, "Kate", 14}}
]
iex(26)> set_id = fn({id, {:user, _, name, age}}) -> {:user, id, name, age} end
#Function<44.97283095/1 in :erl_eval.expr/5>
iex(27)> Enum.zip([10, 20, 30, 40], users) |> Enum.map(set_id)                 
[
  {:user, 10, "Bob", 23},
  {:user, 20, "Helen", 20},
  {:user, 30, "Bill", 15},
  {:user, 40, "Kate", 14}
]
```

Впрочем, никто не запрещает использовать эрланговскую функцию, если вы про нее знаете:
```
iex(30)> set_id = fn(id, {:user, _, name, age}) -> {:user, id, name, age} end    
#Function<43.97283095/2 in :erl_eval.expr/5>
iex(31)> :lists.zipwith(set_id, [10, 20, 30, 40], users)                     
[
  {:user, 10, "Bob", 23},
  {:user, 20, "Helen", 20},
  {:user, 30, "Bill", 15},
  {:user, 40, "Kate", 14}
]
```

Тут есть два нюанса: set_id должна принимать два аргумента, а не кортеж из двух элементов. Порядок аргументов в эрланговских функциях другой. Там список обычно не идет первым аргументом. В Эрланге нет оператора pipe, и порядок аргументов в функциях не подразумевает его использования.


## group_by

Еще одна не редкая задача -- сгруппировать элементы списка по какому-нибудь ключу. Собственно, мы это уже делали, когда делили список пользователей на две группы по возрасту. 

Сделаем то же самое с помощью Enum.group_by:
```
iex(2)> users = HOF.test_data
[
  {:user, 1, "Bob", 23},
  {:user, 2, "Helen", 20},
  {:user, 3, "Bill", 15},
  {:user, 4, "Kate", 14}
]
iex(3)> grouper = fn({:user, _, _, age}) -> 
...(3)> if age > 16, do: :adult, else: :child
...(3)> end
#Function<44.97283095/1 in :erl_eval.expr/5>
iex(4)> Enum.group_by(users, grouper)
%{
  adult: [{:user, 1, "Bob", 23}, {:user, 2, "Helen", 20}],
  child: [{:user, 3, "Bill", 15}, {:user, 4, "Kate", 14}]
}
```

Для каждого элемента списка функция должна определить, к какому ключу он принадлежит. На выходе получится map, где собраны все ключи, и по каждому ключу можно получить список значений, которые к нему относятся.


## chunk_every

Еще одна полезная функция, которая позволяет разбить список на вложенные списки определенного размера. Это удобно при потоковой обработке входящих данных, когда мы хотим обрабатывать эти данные не по одному элементу, а группами по несколько элементов (батчами, чанками). В определенных случаях это увеличивает пропускную способность канала.

```
iex(11)> data = [1,2,3,4,5,6]
[1, 2, 3, 4, 5, 6]
iex(12)> Enum.chunk_every(data, 3)
[[1, 2, 3], [4, 5, 6]]
iex(13)> Enum.chunk_every(data, 2)
[[1, 2], [3, 4], [5, 6]]
```

Пример чуть сложнее:
```
iex(5)> input_bytes = "bla-bla-bla bla-bla bla-bla-bla bla"
"bla-bla-bla bla-bla bla-bla-bla bla"
iex(9)> input_bytes |> :erlang.binary_to_list |> Enum.chunk_every(5) 
['bla-b', 'la-bl', 'a bla', '-bla ', 'bla-b', 'la-bl', 'a bla']
```

Тут мы воспользовались эрланговской функцией :erlang.binary_to_list чтобы преобразовать бинарные данные в список байт.