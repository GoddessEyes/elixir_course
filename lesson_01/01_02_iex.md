# Интерактивная консоль (iex)

Посмотрим на интерактивную консоль **iex** -- второй по важности, после редактора, инструмент разработчика.

Многие языки программирования имеют **REPL** консоль. Она дает возможность работать интеративно: запускать небольшие куски кода и сразу же видеть результ. REPL означает Read, Eval, Print, Loop. То есть, консоль читает код, выполняет его, выводит результат, и опять читает код.

Мы уже начали активно пользоваться консолью когда реализовывали FizzBuzz. Теперь познакомимся с ней поближе:

```shell
~/p/elixir_course_junior/lib/lesson_01 $ iex
Erlang/OTP 23 [erts-11.1] [source] [64-bit] [smp:8:8] [ds:8:8:10] [async-threads:1] [hipe]

Interactive Elixir (1.13.4) - press Ctrl+C to exit (type h() ENTER for help)
iex(1)>
```

Мы видим версию Эрланг: `Erlang/OTP 23`, версию Erlang Runtime System: `erts-11.1` и версию Эликсир: `Elixir (1.13.4)`.


## Компиляция

iex-консоль имеет много встроенных функций. Самые популярные из них имеют однобуквенные называния. 

Мы уже пользовались функцией `c`, которая компилирует и загружает код:

```elixir-iex
iex(1)> c "lib/fizz_buzz_02.exs"
[FizzBuzz02]
iex(2)>
```

Другая популярная функция -- это `h`, которая показывает справку по функциям и модулям Эликсир. Посмотрим справку по функции `c`:

```elixir-iex
iex(1)> h c

                        def c(files, path \\ :in_memory)

Compiles the given files.
...
```

Мы видим, что функция `c` компилирует один или несколько файлов, загружает их в память консоли, или сохраняет сгенерированный байт-код по указаному пути. 

Если мы вносим изменения в код, то с помощью функции `r` мы можем перекомпилировать модуль заново и загрузить свежую версию.

```elixir-iex
iex> r FizzBuzz02
warning: redefining module FizzBuzz02 (current version defined in memory)
  lib/fizz_buzz_02.exs:1

{:reloaded, [FizzBuzz02]}
```

Таким образом удобно вести интерактивную разработку -- писать код и тут же проверять в консоли, как он работает. 

Для больших проектов неудобно перекомпилировать по-отдельности каждый измененный модуль. Поэтому в консоли есть функция `recompile`, которая пересобирает весь проект. Однако, для этого нужно иметь полноценный проект с правильной структурой кода, а не просто несколько модулей.


## Автодополнение

Для кода, который скомпилирован и загружен в консоль, работает автодополнение модулей и функций:

```elixir-iex
iex> Fizz.<tab>
iex> FizzBuzz02.<tab>
iex> FizzBuzz02.m<tab>
iex> FizzBuzz02.main
```

## Интроспекция

Еще одна полезная функция, `i` -- это интроспеция, которая показывает информацию о значениях и переменных:

```elixir-iex
iex> i 42
Term
  42
Data type
  Integer
...

iex> i 3.14
Term
  3.14
Data type
  Float
...

iex> my_str = "Hello"
iex> i my_str
Term
  "Hello"
Data type
  BitString
Byte size
  5
Description
  This is a string: a UTF-8 encoded binary ...

iex> my_list = [1, 2, 3]
iex> i my_list
Term
  [1, 2, 3]
Data type
  List
...
```

## История

Как и большинство других консолей, iex-консоль сохраняет историю команд и позволяет перемещатсья по ней с помощью горячих клавиш `Ctrl-p` и `Ctrl-n` или стрелками.

Также консоль позволяет обратиться к результатам предыдущих команд с помощью функции `v`. `v` принимает номер строки и возвращает результат команды из этой строки. Её также можно вызвать без аргументов, тогда она вернет результат предыдущей команды:

```elixir-iex
iex(9)> 42 * 2
84
iex(10)> v
84
iex(11)> v 9
84
iex(12)> 20 + 30
50
iex(13)> v
50
iex(14)> v 12
50
iex(15)> v 9
84
```


## Справка

Рассмотрим подробнее функцию `h`. Если вызывать ее без аргументов, то мы увидим справку по самой консоли iex. 

Аргументом можно передать имя модуля, функции или макроса. Тогда консоль покажет справку по ним:

```elixir-iex
iex> h
iex> h Map
iex> h Map.get
iex> h Map.<tab>
iex> h Map.fetch
```

В каждый модуль при компиляции автоматически добавляется функция `module_info`, которая возращает некоторые метаданные об этом модуле. Это справедливо как для библиотечных модулей, так и для модулей, которые мы создаем сами:

```elixir-iex
iex> Map.module_info
[
  module: Map,
  exports: [
    __info__: 1,
    delete: 2,
    equal?: 2,
    fetch: 2,
    fetch!: 2,
...

iex> FizzBuzz02.module_info
[
  module: FizzBuzz02,
  exports: [__info__: 1, fizzbuzz: 1, main: 0, module_info: 0, module_info: 1],
  attributes: [vsn: [19328415140430776990959847375304151410]],
  compile: [
...
```

## Справка по собственным модулям

Мы можем добавить документацию в собственные модули, и эта документация будет доступна в iex-консоли. 

Документация для модуля задаётся с помощью аттрибута `@moduledoc`, а для функции с помощью аттрибута `@doc`:

```elixir
defmodule FizzBuzz04 do
  @moduledoc """
  FizzBuzz is a simple task to show basic usage of Elixir.
  """

  def main() do
    fizzbuzz_100()
    |> Enum.join(" ")
    |> IO.puts()
  end

  @doc "Produces list of strings for numbers from 1 to 100."
  @spec fizzbuzz_100() :: [String.t()]
  def fizzbuzz_100() do
    1..100
    |> Enum.map(&fizzbuzz/1)
  end

  @doc "Produces string result for a single number."
  @spec fizzbuzz(integer) :: String.t()
  def fizzbuzz(n) do
    divisible_by_3 = rem(n, 3) == 0
    divisible_by_5 = rem(n, 5) == 0

    cond do
      divisible_by_3 and divisible_by_5 -> "FizzBuzz"
      divisible_by_3 -> "Fizz"
      divisible_by_5 -> "Buzz"
      true -> to_string(n)
    end
  end
end
```

Однако это не работает, если модуль просто скомпилирован из консоли:

```elixir-iex
iex> c "lib/fizz_buzz_04.exs"
iex> h FizzBuzz04
FizzBuzz04 was not compiled with docs
```

Нужно, чтобы скомпилированный байткод был сохранён на диске. Для этого функцию `c` вызываем с двумя аргументами, кроме файла исходников ещё указываем путь, куда сохранять beam-файл:

```elixir-iex
iex> c("lib/fizz_buzz_04.exs", ".")
iex> h FizzBuzz04
                                   FizzBuzz04

FizzBuzz is a simple module to show basic usage of Elixir.

iex> h FizzBuzz04.fizzbuzz_100

                               def fizzbuzz_100()

  @spec fizzbuzz_100() :: [String.t()]

Produces list of strings for numbers from 1 to 100.

iex> h FizzBuzz04.fizzbuzz

                                def fizzbuzz(n)

  @spec fizzbuzz(integer()) :: String.t()

Produces string result for a single number.
```

## runtime_info

Рассмотрим еще функцию `runtime_info`. Длинное название этой функции подразумевает, что она не используется так же часто, как `c`, `h`, `i` или `v`.

Функция выводит полезную информацию о текущем состоянии виртуальной машины: версии Эликсир и Эрлаг, количество планировщиков, потребление памяти, количество процессов и др.

```elixir-iex
iex> runtime_info
## System and architecture

Elixir version:     1.13.4
Erlang/OTP version: 23
...

## Memory

Total:              20 MB
Atoms:              328 KB
...

## Statistics / limits

Uptime:             1 minutes and 40 seconds
...

iex> runtime_info :system

## System and architecture

Elixir version:     1.13.4
Erlang/OTP version: 23
ERTS version:       11.1
Compiled for:       x86_64-unknown-linux-gnu
Schedulers:         8
Schedulers online:  8
...

iex> runtime_info :memory

## Memory

Total:              20 MB
Atoms:              328 KB
Binaries:           111 KB
Code:               7 MB
ETS:                510 KB
Processes:          4 MB
...
```

Как видно, iex-консоль очень полезна, и мы будем активно её использовать на протяжении всего курса.
