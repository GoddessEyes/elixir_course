# Использование Map

При моделировании сущностей не редко обходятся просто Map, не прибегая к созданию Module и Struct для каждого случая.

Часто это бывает при активном использовании JSON как формата обмена данными между клиентом и сервером и между серверами. Map напрямую сериализируется в JSON, и, наоборот, JSON десериализируется в Map. А вот чтобы превратить Struct в JSON и обратно, нужно затратить больше усилий.

Важно знать, что Struct -- это уровень абстракции поверх Map. Эта абстракция существует на этапе компиляции, но в рантайме любая Struct это не более, чем Map с дополнительным ключом, указывающим ее тип:
```
> event = StructExample.create
> event.__struct__
Model.Event.Event
> i event
> event_m = Map.from_struct(event)
> i event_m
```

Поэтому почти все что можно сделать с Map, точно так же можно сделать и со Struct. 

Посмотрим, какие возможности предоставляет Эликсир для работы с Map. 

https://hexdocs.pm/elixir/Map.html


## fetch & get

Для извлечения значений из map обычно используют сопоставление с образцом. Но иногда удобно пользоваться функциями модуля Map. Таких функций несколько, рассмотрим некоторые из них.


### Map.fetch

```
fetch(map(), key()) :: {:ok, value()} | :error
```

```
> event = SimpleExample.create_map()
> Map.fetch(event, :title) 
{:ok, "Team Meeting"}
> Map.fetch(event, :topic)
:error
```

Это каноничный функциональный подход. Ожидается использование либо с ветвлением:
```
case Map.fetch(my_map, some_key) do
  {:ok, value} -> do something
  :error -> do something else
end
```
либо, если отсутствие ключа в map является нештатной ситуаций, то с крашем:
```
> {:ok, title} = Map.fetch(event, :title)
{:ok, "Team Meeting"}
> {:ok, topic} = Map.fetch(event, :topic)
** (MatchError) no match of right hand side value: :error
```


### Map.fetch!

```
fetch!(map(), key()) :: value()
```

```
> title = Map.fetch!(event, :title)      
"Team Meeting"
> topic = Map.fetch!(event, :topic)
** (KeyError) key :topic not found in: %{agenda: ...
```

Это не типично для функционального языка. Видимо, влияние Ruby. Эрлангист или другой разработчик с опытом в ФП всегда напишет:
```
{:ok, topic} = Map.fetch(event, :topic)
``` 
И это даст такой же эффект, как:
```
topic = Map.fetch!(event, :topic)
```

Ну почти такой же. Исключения, как мы видим, разные. И, может быть, KeyError тут более уместно, чем MatchError.


### Map.get

```
get(map, key, default \\ nil)
get(map(), key(), value()) :: value()
```

```
iex(11)> Map.get(event, :title)
"Team Meeting"
iex(12)> Map.get(event, :topic)
nil
iex(13)> Map.get(event, :topic, "No topic")
"No topic"
```

Это удобно, когда нужно передать дефолтное значение для отсутствующего ключа. Тру ФП программист не будет ее использовать в варинате с двумя аргументами. Потому что такой программист считает, что nil не годится как дефолтное значение, и вообще ни для чего не годится. Зато в варианте с тремя аргументами он использует Map.get с удовольствием. 


## put

Чтобы добавить ключ в map, есть функции put, put_new и некоторые другие.

### Map.put

```
put(map(), key(), value()) :: map()
```

```
iex(15)> room = event.location.room
%{floor: 6, number: 610}
iex(16)> Map.put(room, :floor, 7)
%{floor: 7, number: 610}
iex(17)> Map.put(room, :size, :big)
%{floor: 6, number: 610, size: :big}
```

Функция обновляет существующий ключ или добавляет новый.


### Map.put_new

```
put_new(map(), key(), value()) :: map()
```

Если обновление существующего ключа -- нежелательный эффект, т.к. мы не хотим по-ошибке затереть его, сделав опечатку, то лучше воспользоваться функцией put_new. 

```
iex(19)> Map.put_new(room, :size, :big)
%{floor: 6, number: 610, size: :big}
iex(20)> Map.put_new(room, :floor, 7)  
%{floor: 6, number: 610}
```

Поведение с существующим ключом на мой взгляд странное -- функция молча возвращает немодифицированную map. Лучше было бы выбросить исключение.


## update

Рассмотрим несколько функций для обновления значений в map.


### Map.replace

```
replace(map(), key(), value()) :: map()
```

Эта функция дополняет пару **put** и **put_new**. Если **put** и добавляет новый ключ, и обновляет старый, а **put_new** только добавляет новый ключ, то еще нужна функция, которая только обновляет старый ключ. И это функция **replace**.

```
iex(3)> Map.replace(room, :floor, 7)
%{floor: 7, number: 610}
iex(4)> Map.replace(room, :size, :big)
%{floor: 6, number: 610}
```

Как и put_new, эта функция молча возвращает неизмененную map, если нужного ключа в ней нет.

Но еще есть функция **replace!**, которая бросает исключение:
```
iex(5)> Map.replace!(room, :floor, 7) 
%{floor: 7, number: 610}
iex(6)> Map.replace!(room, :size, :big)
** (KeyError) key :size not found in: ...
```

Для полноты АПИ модуля Map было бы правильно иметь и функцию **put_new!**, но её нет.


### Map.update/4

```
update(
  map(),
  key(),
  default :: value(),
  (existing_value :: value() -> updated_value :: value())
) :: map()
```

Полезная функция, но с довольно сложными аргументами, с которыми нужно разобраться.

Рассмотрим пример. Допустим, мы хотим добавить в наше событие еще одного участника. 
```
> new_participant = %{name: "Elon Musk", role: :ceo, species: :human}
```

Это можно сделать так:
```
> new_participans = [new_participant | event.participants]
> Map.put(event, :participants, new_participans)
```

Нам пришлось извлечь participants из event, модифицировать, затем вставить обратно. Эти шаги можно сделать одним вызовом update.

```
> Map.update(event, :participants, [], fn p -> [new_participant | p] end)
```

Первый аргумент -- map, второй -- ключ, значение которого нужно обновить. Четвертый аргумент -- анонимная функция, которая принимает старое значение ключа, и должна вернуть новое значение ключа. 

Тут есть еще третий аргумент, который используется как значение по умолчанию, если данного ключа нет в map. Тогда ключ добавляется с этим значением, а анонимная функция не вызывается.

Есть еще функция **update!** с тремя аргументами, которая бросает исключение в случае отсутствия ключа, и значение по умолчанию ей не нужно.
```
> Map.update!(event, :participants, fn p -> [new_participant | p] end)
> Map.update!(event, :party, fn p -> [new_participant | p] end)       
** (KeyError) key :party not found in:
```


## split, take, drop

Эта группа функций не самая популярная, но полезная в некоторых случаях. Работа с нашим event не такой случай, тут нужен другой пример.

Допустим, у нас есть map с населением крупных городов Европы:
```
iex(1)> population = EuropeCity.population()
%{
  "Barcelona" => 1636762,
  "Belgrade" => 1397939,
  "Berlin" => 3748148,
```

И список городов, находящихся в странах -- членах Евросоюза:
```
iex(2)> eu_cities = EuropeCity.eu_cities()
["Berlin", "Madrid", "Rome",
```

С помощью Map.take мы можем выбрать информацию по населению только тех городов, которые входят в Евросоюз:
```
iex(3)> Map.take(population, eu_cities)
```
Функция принимает map и список ключей, отбрасывает из map ключи, не входящие в список, и возвращает новую map. Если в списке есть ключи, которых нет в map, то они просто игнорируются.

Map.drop работает противоположным образом, возвращает map с ключами, не входящими в список.
```
iex(4)> Map.drop(population, eu_cities)
```
То есть, мы получили население городов, не входящих в Евросоюз.

Функция Map.split объединяет take и drop. Она разделяет map на две новые map. Первая содержит ключи из списка, вторая не содержит.
```
iex(5)> Map.split(population, eu_cities)
```

## merge

Мы разобрались, как разделить map на две. Теперь посмотрим на противоположную операцию -- объединение двух map в одну.

Для примера возьмем любимую музыку, мою:
```
iex(1)> yura_fs = BestSong.yura_favorite_songs()
%{
  "Bel Suono" => "Libertango",
  "Péter Bence" => "Despacito",
  "The Manhattan Transfer" => "The Offbeat of Avenues"
}
```
и кота Тихона:
```
iex(2)> tihon_fs = BestSong.tihon_favorite_songs()
%{
  "Electric Light Orchestra" => "Mr. Blue Sky",
  "Péter Bence" => "Africa",
  "The Manhattan Transfer" => "The Junction"
}
```

Как видно, у наши вкусы частично пересекаются. Мы имеем две map по три ключа в каждой, причем один ключ уникальный, а два конфликтуют. 

Посмотрим, что будет, если применить функцию Map.merge:
```
iex(3)> Map.merge(yura_fs, tihon_fs)
%{
  "Bel Suono" => "Libertango",
  "Electric Light Orchestra" => "Mr. Blue Sky",
  "Péter Bence" => "Africa",
  "The Manhattan Transfer" => "The Junction"
}
iex(4)> Map.merge(tihon_fs, yura_fs)
%{
  "Bel Suono" => "Libertango",
  "Electric Light Orchestra" => "Mr. Blue Sky",
  "Péter Bence" => "Despacito",
  "The Manhattan Transfer" => "The Offbeat of Avenues"
}
```
Функция объединяет две map в одну. И если есть конфликтующие ключи, то значения для них берутся из map, которая передана вторым аргументом.

Это не всегда подходящая стратегия разрешения конфликтов. Если у нас есть стратегия лучше, то мы можем воспользоваться Map.merge/3, где третим аргументом передается функция, разрешающая конфликт:

```
Map.merge(yura_fs, tihon_fs, fn (_artist, song_1, song_2) -> [song_1, song_2] end)
%{
  "Bel Suono" => "Libertango",
  "Electric Light Orchestra" => "Mr. Blue Sky",
  "Péter Bence" => ["Despacito", "Africa"],
  "The Manhattan Transfer" => ["The Offbeat of Avenues", "The Junction"]
}
``` 

Как видно, функция принимает ключ и оба конфликтующих значения, и должна вернуть новое значение.


## map, reduce, filter

Напоследок вспомним, что все функции модуля Enum, которые мы изучали в 5-м уроке, работают с map.

```
iex(10)> Enum.map(yura_fs, fn ({artist, _song}) -> artist end)
["Bel Suono", "Péter Bence", "The Manhattan Transfer"]

iex(14)> Enum.filter(population, fn({_, p}) -> p > 5_000_000 end)    
[
  {"Istanbul", 15462452},
  {"London", 9126366},
  {"Moscow", 12195221},
  {"Saint Petersburg", 5383890}
]

iex(17)> Enum.reduce(population, 0, fn({city, p}, acc) ->
...(17)> if city in eu_cities, do: acc + p, else: acc
...(17)> end)
25684115
```

Это работает потому, что модуль Enum поддерживает любые структуры данных, реализующие протокол Enumerable. Наша следущая тема -- **протокол**. Разберемся, что это такое.