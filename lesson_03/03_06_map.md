### Map

key-value структура данных, аналогичная словарю (dictionary) в Python или HashMap в Java.

Maps are the go-to key/value data structure in Elixir.
They have good performance at all sizes.

Основной синтаксис:
```
m = %{"a" => 42, "b" => 100}
m["a"]
m["b"]
```
Синтаксический сахар для ключей-атомов:
```
m = %{a: 45, b: 100}
m.a
m.b
```
сахар удобный, и он поощряет использовать атомы в роли ключей.
Но ключом может быть любое значение, тк все значения иммутабельны.

Разное поведение при доступе к несуществующему ключу:
```
iex(5)> m.c
** (KeyError) key :c not found in: %{a: 45, b: 100}

iex(5)> m[:c]
nil
```

```
map = %{ name: "Dave", likes: "Programming", where: "Dallas" }

Map.keys(map)
Map.values(map)

map[:name]
map.name

map1 = Map.drop(map, [:where, :likes])
Map.take

map2 = Map.put(map, :also_likes, "Ruby")
```

TODO: хорошие примеры

Key access
```
Map.get(my_map, key) # same as [], but you can specify the default value
Map.fetch -> {:ok, val} | :error
Map.fetch! -> throws KeyError
```

## Updating a map

Map is immutable, and so the result of the update is a new map.

```
Map.put

new_map = %{ old_map | key1 => value1, key2 => value2 }
```
This creates a new map that is a copy of the old,
but the values associated with the keys on the right of the pipe character are updated.

However, this syntax will not add a new key to a map.
To do this, you have to use the Map.put_new/3 function.

throws KeyError


## fetch & get

Для извлечения значений из map обычно используют сопоставление с образцом. Но иногда удобно пользоваться функциями модуля Map. Таких функций несколько, рассмотрим некоторые из них.


### Map.fetch

```
fetch(map(), key()) :: {:ok, value()} | :error
```

```
> event = SimpleExample.create_map()
> Map.fetch(event, :title)
{:ok, "Team Meeting"}
> Map.fetch(event, :topic)
:error
```

Это каноничный функциональный подход. Ожидается использование либо с ветвлением:
```
case Map.fetch(my_map, some_key) do
  {:ok, value} -> do something
  :error -> do something else
end
```
либо, если отсутствие ключа в map является нештатной ситуаций, то с крашем:
```
> {:ok, title} = Map.fetch(event, :title)
{:ok, "Team Meeting"}
> {:ok, topic} = Map.fetch(event, :topic)
** (MatchError) no match of right hand side value: :error
```


### Map.fetch!

```
fetch!(map(), key()) :: value()
```

```
> title = Map.fetch!(event, :title)
"Team Meeting"
> topic = Map.fetch!(event, :topic)
** (KeyError) key :topic not found in: %{agenda: ...
```

Это не типично для функционального языка. Видимо, влияние Ruby.  или другой разработчик с опытом в ФП всегда напишет:
```
{:ok, topic} = Map.fetch(event, :topic)
```
И это даст такой же эффект, как:
```
topic = Map.fetch!(event, :topic)
```

Ну почти такой же. Исключения, как мы видим, разные. И, может быть, KeyError тут более уместно, чем MatchError.


### Map.get

```
get(map, key, default \\ nil)
get(map(), key(), value()) :: value()
```

```
iex(11)> Map.get(event, :title)
"Team Meeting"
iex(12)> Map.get(event, :topic)
nil
iex(13)> Map.get(event, :topic, "No topic")
"No topic"
```

Это удобно, когда нужно передать дефолтное значение для отсутствующего ключа. Тру ФП программист не будет ее использовать в варинате с двумя аргументами. Потому что такой программист считает, что nil не годится как дефолтное значение, и вообще ни для чего не годится. Зато в варианте с тремя аргументами он использует Map.get с удовольствием.


## put

Чтобы добавить ключ в map, есть функции put, put_new и некоторые другие.

### Map.put

```
put(map(), key(), value()) :: map()
```

```
iex(15)> room = event.location.room
%{floor: 6, number: 610}
iex(16)> Map.put(room, :floor, 7)
%{floor: 7, number: 610}
iex(17)> Map.put(room, :size, :big)
%{floor: 6, number: 610, size: :big}
```

Функция обновляет существующий ключ или добавляет новый.


### Map.put_new

```
put_new(map(), key(), value()) :: map()
```

Если обновление существующего ключа -- нежелательный эффект, т.к. мы не хотим по-ошибке затереть его, сделав опечатку, то лучше воспользоваться функцией put_new.

```
iex(19)> Map.put_new(room, :size, :big)
%{floor: 6, number: 610, size: :big}
iex(20)> Map.put_new(room, :floor, 7)
%{floor: 6, number: 610}
```

Поведение с существующим ключом на мой взгляд странное -- функция молча возвращает немодифицированную map. Лучше было бы выбросить исключение.


## update

Рассмотрим несколько функций для обновления значений в map.


### Map.replace

```
replace(map(), key(), value()) :: map()
```

Эта функция дополняет пару **put** и **put_new**. Если **put** и добавляет новый ключ, и обновляет старый, а **put_new** только добавляет новый ключ, то еще нужна функция, которая только обновляет старый ключ. И это функция **replace**.

```
iex(3)> Map.replace(room, :floor, 7)
%{floor: 7, number: 610}
iex(4)> Map.replace(room, :size, :big)
%{floor: 6, number: 610}
```

Как и put_new, эта функция молча возвращает неизмененную map, если нужного ключа в ней нет.

Но еще есть функция **replace!**, которая бросает исключение:
```
iex(5)> Map.replace!(room, :floor, 7)
%{floor: 7, number: 610}
iex(6)> Map.replace!(room, :size, :big)
** (KeyError) key :size not found in: ...
```

Для полноты АПИ модуля Map было бы правильно иметь и функцию **put_new!**, но её нет.


## Упражнение

Посчитать, сколько раз встречается каждое слово в строке.
```
iex(1)> c "lib/lesson_03/task_03_06_map.exs"
[Lesson_03.Task_03_06_Map]
iex(2)> alias Lesson_03.Task_03_06_Map, as: TM
Lesson_03.Task_03_06_Map
iex(3)> str = TM.test_string
"Elixir in Action is a tutorial book that aims to bring developers\nnew to Elixir and Erlang to the point where they can develop complex systems on their own.\n"
iex(4)> TM.count_words(str)
%{
  "Action" => 1,
  "Elixir" => 2,
  "Erlang" => 1,
  "a" => 1,
  "aims" => 1,
  "and" => 1,
  "book" => 1,
  "bring" => 1,
  "can" => 1,
  "complex" => 1,
  "develop" => 1,
  "developers" => 1,
  "in" => 1,
  "is" => 1,
  "new" => 1,
  "on" => 1,
  "own." => 1,
  "point" => 1,
  "systems" => 1,
  "that" => 1,
  "the" => 1,
  "their" => 1,
  "they" => 1,
  "to" => 3,
  "tutorial" => 1,
  "where" => 1
}
```

Запуск теста:
```
elixir lib/lesson_03/task_03_06_map.exs
```

