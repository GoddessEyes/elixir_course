# Словари (Map)

key-value структура данных, аналогичная словарю (dictionary) в Python или HashMap в Java.

Maps are the go-to key/value data structure in Elixir.
They have good performance at all sizes.

Основной синтаксис:

```elixir
m = %{"a" => 42, "b" => 100}
m["a"]
m["b"]
```

Синтаксический сахар для ключей-атомов:

```elixir
m = %{a: 45, b: 100}
m.a
m.b
```

Сахар удобный, и он поощряет использовать атомы в роли ключей.
Но ключом может быть любое значение, т.к. все значения иммутабельны.

Разное поведение при доступе к несуществующему ключу:

```elixir-iex
iex(5)> m.c
** (KeyError) key :c not found in: %{a: 45, b: 100}

iex(5)> m[:c]
nil
```

```elixir-iex
iex(1)> map = %{name: "Dave", likes: "Programming", where: "Dallas"}
%{name: "Dave", where: "Dallas", likes: "Programming"}

iex(2)> Map.keys(map)
[:name, :where, :likes]
iex(3)> Map.values(map)
["Dave", "Dallas", "Programming"]

iex(4)> map[:name]
"Dave"
iex(5)> map.name
"Dave"

iex(6)> Map.drop(map, [:where, :likes])
%{name: "Dave"}
iex(7)> Map.take(map, [:where, :likes])
%{where: "Dallas", likes: "Programming"}
iex(8)> Map.put(map, :also_likes, "Ruby")
%{name: "Dave", where: "Dallas", likes: "Programming", also_likes: "Ruby"}
```

TODO: хорошие примеры

## fetch & get

```elixir
Map.get(my_map, key) # same as [], but you can specify the default value
Map.fetch -> {:ok, val} | :error
Map.fetch! -> throws KeyError
```

Для извлечения значений из map обычно используют сопоставление с образцом. Но иногда удобно пользоваться функциями модуля Map. Таких функций несколько, рассмотрим некоторые из них.

### Map.fetch

```elixir
fetch(map(), key()) :: {:ok, value()} | :error
```

```
> event = SimpleExample.create_map()
> Map.fetch(event, :title)
{:ok, "Team Meeting"}
> Map.fetch(event, :topic)
:error
```

Это каноничный функциональный подход. Ожидается использование либо с ветвлением:

```elixir
case Map.fetch(my_map, some_key) do
  {:ok, value} -> do something
  :error -> do something else
end
```

либо, если отсутствие ключа в map является нештатной ситуаций, то с крашем:

```elixir
> {:ok, title} = Map.fetch(event, :title)
{:ok, "Team Meeting"}
> {:ok, topic} = Map.fetch(event, :topic)
** (MatchError) no match of right hand side value: :error
```

### Map.fetch!

```elixir
fetch!(map(), key()) :: value()
```

```
> title = Map.fetch!(event, :title)
"Team Meeting"
> topic = Map.fetch!(event, :topic)
** (KeyError) key :topic not found in: %{agenda: ...
```

Это не типично для функционального языка. Видимо, влияние Ruby. Другой разработчик с опытом в ФП всегда напишет:

```elixir
{:ok, topic} = Map.fetch(event, :topic)
```

И это даст такой же эффект, как:

```elixir
topic = Map.fetch!(event, :topic)
```

Ну, почти такой же. Исключения будут разные. И, может быть, KeyError тут более уместно, чем MatchError.


### Map.get

```elixir
get(map, key, default \\ nil)
get(map(), key(), value()) :: value()
```

```elixir-iex
iex(11)> Map.get(event, :title)
"Team Meeting"
iex(12)> Map.get(event, :topic)
nil
iex(13)> Map.get(event, :topic, "No topic")
"No topic"
```

Это удобно, когда нужно передать дефолтное значение для отсутствующего ключа. Тру ФП программист не будет ее использовать в варинате с двумя аргументами. Потому что такой программист считает, что nil не годится как дефолтное значение, и вообще ни для чего не годится. Зато в варианте с тремя аргументами он использует Map.get с удовольствием.

## put

Чтобы добавить ключ в map, есть функции put, put_new и некоторые другие.

### Map.put

```elixir
put(map(), key(), value()) :: map()
```

```elixir-iex
iex(15)> room = event.location.room
%{floor: 6, number: 610}
iex(16)> Map.put(room, :floor, 7)
%{floor: 7, number: 610}
iex(17)> Map.put(room, :size, :big)
%{floor: 6, number: 610, size: :big}
```

Функция обновляет существующий ключ или добавляет новый.

### Map.put_new

```elixir
put_new(map(), key(), value()) :: map()
```

Если обновление существующего ключа -- нежелательный эффект, т.к. мы не хотим по-ошибке затереть его, сделав опечатку, то лучше воспользоваться функцией put_new.

```elixir-iex
iex(19)> Map.put_new(room, :size, :big)
%{floor: 6, number: 610, size: :big}
iex(20)> Map.put_new(room, :floor, 7)
%{floor: 6, number: 610}
```

Поведение с существующим ключом, на мой взгляд, странное -- функция молча возвращает немодифицированную map. Лучше было бы выбросить исключение.

## update

Рассмотрим несколько функций для обновления значений в map.

### Map.replace

```elixir
replace(map(), key(), value()) :: map()
```

Эта функция дополняет пару **put** и **put_new**. Если **put** и добавляет новый ключ, и обновляет старый, а **put_new** только добавляет новый ключ, то еще нужна функция, которая только обновляет старый ключ. И это функция **replace**.

```elixir-iex
iex(3)> Map.replace(room, :floor, 7)
%{floor: 7, number: 610}
iex(4)> Map.replace(room, :size, :big)
%{floor: 6, number: 610}
```

Как и put_new, эта функция молча возвращает неизмененную map, если нужного ключа в ней нет.

Но еще есть функция **replace!**, которая бросает исключение:

```elixir-iex
iex(5)> Map.replace!(room, :floor, 7)
%{floor: 7, number: 610}
iex(6)> Map.replace!(room, :size, :big)
** (KeyError) key :size not found in: ...
```

Для полноты АПИ модуля Map было бы правильно иметь и функцию **put_new!**, но её нет.

## Упражнение

Посчитать, сколько раз встречается каждое слово в строке.

```elixir-iex
iex(1)> c "lib/map_example.exs"
[MapExample, MapExampleTest]
iex(2)> str = MapExample.test_string
"Elixir in Action is a tutorial book that aims to bring developers\nnew to Elixir and Erlang to the point where they can develop complex systems on their own.\n"
iex(3)> MapExample.count_words(str)
%{
  "Action" => 1,
  "Elixir" => 2,
  "Erlang" => 1,
  "a" => 1,
  "aims" => 1,
  "and" => 1,
  "book" => 1,
  "bring" => 1,
  "can" => 1,
  "complex" => 1,
  "develop" => 1,
  "developers" => 1,
  "in" => 1,
  "is" => 1,
  "new" => 1,
  "on" => 1,
  "own." => 1,
  "point" => 1,
  "systems" => 1,
  "that" => 1,
  "the" => 1,
  "their" => 1,
  "they" => 1,
  "to" => 3,
  "tutorial" => 1,
  "where" => 1
}
```

Запуск теста:

```shell
elixir lib/map_example.exs
.
Finished in 0.04 seconds (0.04s on load, 0.00s async, 0.00s sync)
1 test, 0 failures

Randomized with seed 145339
```

