# Булевые значения (Boolean)

Булевый тип в Эликсир представлен значениями `true` и `false`. Сюда же можно отнести и значение `nil`, выражающее отсутствие информации. 

На самом деле все эти три значения являются атомами, но язык интерпретирует их отдельно от остальных атомов и реализует для них операторы двоичной логики.

## Строгие булевые операторы

Строгие операторы `and`, `or`, `not` работают только со значениями `true` и `false`:

```elixir-iex
iex(1)> true and true
true
iex(2)> true and false
false
iex(3)> true or true
true
iex(4)> true or false
true
iex(5)> not true
false
iex(6)> not false
true
```

Попробуем с не-булевыми значениями:

```elixir-iex
iex(1)> 1 and true
** (BadBooleanError) expected a boolean on left-side of "and", got: 1
    iex:1: (file)
iex(1)> 1 or true
** (BadBooleanError) expected a boolean on left-side of "or", got: 1
    iex:1: (file)
iex(1)> 1 and false
** (BadBooleanError) expected a boolean on left-side of "and", got: 1
    iex:1: (file)
iex(1)> 1 or false
** (BadBooleanError) expected a boolean on left-side of "or", got: 1
    iex:1: (file)
iex(1)> true and 1
1
iex(2)> true or 1
true
iex(3)> false and 1
false
iex(4)> false or 1
1
```

Как видим, эта строгость срабатывает только если не-булевое значение стоит справа. А если слева, то срабатывает ленивость этих операторов, которые не вычисляют всё выражение целиком, если без этого можно обойтись.


## Мягкие булевые операторы

Мягкие (relaxed) операторы `&&`, `||`, `!` работают с любыми значениями. При этом `false` и `nil` интерпретируются как `false`, а любые другие значения интерпретируются как `true`. Пустой список и пустой словарь тоже интерпретируются как `true`. Это отличает Эликсир от других языков, например от Python.

В английской терминологии в контексте мягких операторов значения `false` и `nil` называют **falsy**, а все остальные значения называют **truthy**. Русский перевод подобрать трудно :)

```elixir
iex(5)> 1 && true
true
iex(6)> 1 || nil
1
iex(7)> ! 42
false
iex(8)> ! nil
true
```

Важно помнить, что в некоторых языках пустые списки `[]` и пустые словари `%{}` рассматриваются как `false` а не пустые как `true`. Например, в Python:

```python
>>> if []:
...   print('hello')
...
>>>
>>> if [1]:
...   print('hello')
...
hello
```

В Эликсир это не так. Любой список и словарь, пустой и не пустой, будет рассматриваться как `true`:

```elixir-iex
iex(1)> if true, do: IO.puts('hello')
hello
:ok
iex(2)> if [], do: IO.puts('hello')
hello
:ok
iex(3)> if [1], do: IO.puts('hello')
hello
:ok
iex(4)> if false, do: IO.puts('hello')
nil
```


## Упражнение

Три значения `true`, `false` и `nil` на самом деле дают не двоичную, а троичную логику. В Эликсир нет операторов троичной логики, но не сложно реализовать соответствующие функции.

Есть разные варианты троичной логики, мы реализуем [логику Стивена Клини](https://ru.wikipedia.org/wiki/%D0%A2%D1%80%D0%BE%D0%B8%D1%87%D0%BD%D0%B0%D1%8F_%D0%BB%D0%BE%D0%B3%D0%B8%D0%BA%D0%B0), основываясь на таблицах истинности:


Таблица **NOT(A)**

| A     | NOT A |
|-------|-------|
| false | true  |
| nil   | nil   |
| true  | false |


Таблица **AND(A, B)**

| AND   | false | nil   | true  |
| false | false | false | false |
| nil   | false | nil   | nil   |
| true  | false | nil   | true  |


Таблица **OR(A, B)**

| OR    | false | nil  | true |
| false | fasle | nil  | true |
| nil   | nil   | nil  | true |
| true  | true  | true | true |

Для начала мы просто реализуем эти таблицы в коде прямо в том виде, в каком они описаны. Это будет первый шаг:

```elixir
defmodule BoolExample do
  defmodule Step1 do
    def sk_not(false), do: true
    def sk_not(nil), do: nil
    def sk_not(true), do: false

    def sk_and(false, false), do: false
    def sk_and(false, nil), do: false
    def sk_and(false, true), do: false
    def sk_and(nil, false), do: false
    def sk_and(nil, nil), do: nil
    def sk_and(nil, true), do: nil
    def sk_and(true, false), do: false
    def sk_and(true, nil), do: nil
    def sk_and(true, true), do: true

    def sk_or(false, false), do: false
    def sk_or(false, nil), do: nil
    def sk_or(false, true), do: true
    def sk_or(nil, false), do: nil
    def sk_or(nil, nil), do: nil
    def sk_or(nil, true), do: true
    def sk_or(true, false), do: true
    def sk_or(true, nil), do: true
    def sk_or(true, true), do: true
  end
end
```

Добавим тесты:

```elixir
ExUnit.start()

defmodule BoolExampleTest do
  use ExUnit.Case
  import BoolExample.Step1

  test "Stephen Kleene, not" do
    assert sk_not(true) == false
    assert sk_not(nil) == nil
    assert sk_not(false) == true
  end

  test "Stephen Kleene, and" do
    assert sk_and(false, false) == false
    assert sk_and(false, nil) == false
    assert sk_and(false, true) == false
    assert sk_and(nil, false) == false
    assert sk_and(nil, nil) == nil
    assert sk_and(nil, true) == nil
    assert sk_and(true, false) == false
    assert sk_and(true, nil) == nil
    assert sk_and(true, true) == true
  end

  test "Stephen Kleene, or" do
    assert sk_or(false, false) == false
    assert sk_or(false, nil) == nil
    assert sk_or(false, true) == true
    assert sk_or(nil, false) == nil
    assert sk_or(nil, nil) == nil
    assert sk_or(nil, true) == true
    assert sk_or(true, false) == true
    assert sk_or(true, nil) == true
    assert sk_or(true, true) == true
  end
end
```

И убедимся, что всё работает правильно:

```shell
$ elixir lib/bool_example.exs
...
Finished in 0.4 seconds (0.3s on load, 0.00s async, 0.03s sync)
3 tests, 0 failures

Randomized with seed 807452
```

TODO Step2 -- оптимизируем реализацию.

```
iex(1)> c "lib/lesson_03/task_03_03_bool.exs"
[Lesson_03.Task_03_03_Bool]
iex(2)> alias Lesson_03.Task_03_03_Bool, as: TB
Lesson_03.Task_03_03_Bool
iex(3)> TB.sk_not true
false
iex(4)> TB.sk_and true, nil
nil
iex(5)> TB.sk_or true, nil
true
```

Запуск тестов:
```
elixir lib/lesson_03/task_03_03_bool.exs
```
