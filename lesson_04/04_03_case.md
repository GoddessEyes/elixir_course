# Условные переходы (Control Flow)

Условные переходы в функциональных языках сильно отличаются от императивных, потому что основаны на сопоставлении с образцом. Основная идея в том, что некое значение по-очереди сравнивается с несколькими шаблонами, и в зависимости от того, с каким шаблоном оно совпадет, выполняется та или иная ветка кода.

Есть несколько вариантов условных переходов:
- конструкция case
- клозы функций (clause)
- обработка исключений (resque, catch)
- чтение сообщений из mailbox (receive)

Все они реализуют эту идею.


## Конструкция case

Начнем с примеров, которые мы уже видели:

```elixir
def gcd(a, b) do
  case rem(a, b) do
    0 -> b
    c -> gcd(b, c)
  end
end
```

Здесь вычисляется значение **rem(a, b)**, и сравнивается с двумя шаблонами. Первый шаблон -- литерал **0**. Если значение совпадает с ним, то выполняется код, завершающий рекурсию и возвращающий **b**. Второй шаблон -- переменная **c**. С этим шаблоном совпадут любые значения. И выполняется код -- вызов **gcd(b, c)**.

Второй пример:

```elixir
case Map.fetch(acc, word) do
  {:ok, count} -> Map.put(acc, word, count + 1)
  :error -> Map.put(acc, word, 1)
end
```

Здесь выполняется вызов функции **Map.fetch(acc, word)**. Получившееся значение сравнивается с двумя шаблонами и выполняется соответствующий код.

Шаблонов может быть несколько. И важен их порядок, потому что первый совпавший шаблон останавливает перебор оставшихся шаблонов. Если не совпал ни один из шаблонов, то генерируется исключение.

В общем виде конструкция **case** выглядит так:

```elixir
case Expr do
    Pattern1 [when GuardSequence1] ->
        Body1
    ...
    PatternN [when GuardSequenceN] ->
        BodyN
end
```

Что такое GuardSequence -- цепочка охранных выражений, мы рассмотрим позже.

case могут быть вложенными друг в друга:

```elixir
  def handle(animal, action) do
    case animal do
      {:dog, name} ->
        case action do
          :feed -> IO.puts("feed the dog #{name}")
          :pet -> IO.puts("pet the dog #{name}")
        end
      {:cat, name} ->
        case action do
          :feed -> IO.puts("feed the cat #{name}")
          :pet -> IO.puts("pet the cat #{name}")
        end
    end
  end
```

TODO: показать badmatch, передать неправильный animal и неправильный action.

Вложенный даже на два уровня код плохо читается. Обычно этого можно избежать. C помощью pattern matching можно проверить одновременно несколько условий. Данный пример можно реализовать без вложенного case таким образом:

```elixir
  def handle2(animal, action) do
    case {animal, action} do
      {{:dog, name}, :feed} -> IO.puts("feed the dog #{name}")
      {{:dog, name}, :pet} -> IO.puts("pet the dog #{name}")
      {{:cat, name}, :feed} -> IO.puts("feed the cat #{name}")
      {{:cat, name}, :pet} -> IO.puts("pet the cat #{name}")
    end
  end
```


## Клозы функций (Clause)

Вторая по популярности конструкция (или даже первая, в зависимости от предпочтений разработчика), это использование клозов функций.

Этот термин пишется **clause**, произносится **[klôz]** и означает одно из нескольких тел функции.

В Эликсир одна функция может иметь несколько тел -- несколько разных блоков кода. В зависимости от входящих аргументов выполняется только один из этих блоков.

```elixir
  def handle3({:dog, name}, :feed) do
    IO.puts("feed the dog #{name}")
  end
  def handle3({:dog, name}, :pet) do
    IO.puts("pet the dog #{name}")
  end
  def handle3({:cat, name}, :feed) do
    IO.puts("feed the cat #{name}")
  end
  def handle3({:cat, name}, :pet) do
    IO.puts("pet the cat #{name}")
  end
```

Шаблоны описываются прямо в аргументах функции, отдельно для каждого тела. Принцип такой же, как и с конструкцией **case** -- шаблоны проверяются по очереди на совпадение с входящими аргументами функции. Первый совпавший шаблон вызывает соответствующий блок кода и останавливает дальшейший перебор. Если ни один шаблон не совпал, то генерируется исключение.

TODO: показать catch all clause

Как и в случае с **case**, здесь тоже важна очередность шаблонов. Типичная ошибка -- расположить более общий шаблон выше, чем более специфичный шаблон:

```elixir
  def handle3(animal, action) do
    IO.puts("do something")
  end
  def handle3({:dog, name}, :feed) do
    IO.puts("feed the dog #{name}")
  end

```

Во многих таких случаях компилятор выдаст предупреждение:

```
warning: this clause for handle3/2 cannot match because a previous clause at line 27 always matches
  lib/control_flow.exs:30
```

Но бывает, что компилятор не замечает проблему.

TODO: показать пример с непопадаем в клоз.


## Охранные выражения (Guards)

Теперь вернемся к упомянутым выше охранным выражениям.

Не всегда достаточно шаблона, чтобы проверить все условия для ветвления в коде. Например, шаблоном нельзя проверить попадание числа в определенный диапазон.

```elixir
  def handle4(animal) do
    case animal do
      {:dog, name, age} when age > 10 -> IO.puts("#{name} is a dog older than 10")
      {:dog, name, _} -> IO.puts("#{name} is a 10 years old or younger dog")
      {:cat, name, age} when age > 10 -> IO.puts("#{name} is a cat older than 10")
      {:cat, name, _} -> IO.puts("#{name} is a 10 years old or younger cat")
    end
  end
```

Охранные выражения могут использоваться и с **case**, и с телами функций:

```elixir
  def handle5({:dog, name, age}) when age > 10 do
    IO.puts("#{name} is a dog older than 10")
  end
  def handle5({:dog, name, _age}) do
    IO.puts("#{name} is a 10 years old or younger dog")
  end
  def handle5({:cat, name, age}) when age > 10 do
    IO.puts("#{name} is a cat older than 10")
  end
  def handle5({:cat, name, _age}) do
    IO.puts("#{name} is a 10 years old or younger cat")
  end
```

Охранное выражение представляет собой предикат, или цепочку предикатов:

```elixir
when predicat1 and predicat2 or ... predicatN ->
```

В предикатах можно использовать ограниченный набор функций, описанный в [документации](https://hexdocs.pm/elixir/patterns-and-guards.html#list-of-allowed-functions-and-operators). Некоторые функциональные языки разрешают вызывать любые функции в охранных выражениях. Но Эликсир (и Эрланг) не относятся к таким языкам.

Если при вычислении охранного выражения возникает исключение, то оно не приводит к остановке процесса, а приводит к тому, что все выражение вычисляется в false:

```elixir
  def handle6(num) when 10 / num > 2 do
    IO.puts("clause 1")
  end
  def handle6(num) do
    IO.puts("clause 2")
  end
```

Это позволяет писать выражения проще. Вместо:

```elixir
when is_map(a) and map_size(a) > 10 ->
```

можно сразу писать:

```elixir
when map_size(a) > 10 ->
```
