# Условные переходы (Control Flow)

Условные переходы в функциональных языках сильно отличаются от императивных, потому что основаны на сопоставлении с образцом. Основная идея в том, что некое значение по-очереди сравнивается с несколькими шаблонами, и в зависимости от того, с каким шаблоном оно совпадет, выполняется та или иная ветка кода.

Есть несколько вариантов условных переходов:
- конструкция case
- клозы функций (clause)
- обработка исключений (resque, catch)
- чтение сообщений из mailbox (receive)

Все они реализуют эту идею.

## Конструкция case

Вспомним примеры, который мы уже видели.

На 3-м уроке мы реализовывали вычисление наибольшего общего делителя (Greatest Common Divisor, GCD):

```elixir
def gcd(a, b) do
  case rem(a, b) do
    0 -> b
    c -> gcd(b, c)
  end
end
```

Здесь вычисляется значение **rem(a, b)** и сравнивается с двумя шаблонами. Первый шаблон -- литерал **0**. Если значение совпадает с ним, то выполняется код, завершающий рекурсию и возвращающий **b**. Второй шаблон -- переменная **c**. С этим шаблоном совпадут любые значения. И выполняется код -- вызов **gcd(b, c)**.

И также на 3-м уроке мы считали количество слов в строке:

```elixir
defp count_words([word | words], acc) do
  acc = case Map.fetch(acc, word) do
    {:ok, count} -> Map.put(acc, word, count + 1)
    :error -> Map.put(acc, word, 1)
  end
  count_words(words, acc)
end
```

Здесь выполняется вызов функции **Map.fetch(acc, word)**. Получившееся значение сравнивается с двумя шаблонами, и выполняется соответствующий код.

Шаблонов может быть несколько. И важен их порядок, потому что первый совпавший шаблон останавливает перебор оставшихся шаблонов. Если не совпал ни один из шаблонов, то генерируется исключение.

В общем виде конструкция **case** выглядит так:

![case](./img/case.png)

Что такое Guard (охранное выражение), мы рассмотрим позже.

**case** могут быть вложенными друг в друга. Рассмотрим пример, с домашними животными. У нас есть три вида животных: собака, кошка и крыса. И мы может совершать с ними два действия: кормить и гладить.

Реализуем ветвления в коде, которые зависят от вида животного и от действия:

```elixir
defmodule ControlFlow do

  def handle(animal, action) do
    case animal do
      {:dog, name} ->
        case action do
          :feed -> IO.puts("feed the dog #{name}")
          :pet -> IO.puts("pet the dog #{name}")
        end
      {:cat, name} ->
        case action do
          :feed -> IO.puts("feed the cat #{name}")
          :pet -> IO.puts("pet the cat #{name}")
        end
      {:rat, name} ->
        case action do
          :feed -> IO.puts("feed the rat #{name}")
          :pet -> IO.puts("pet the rat #{name}")
        end
    end
  end

end
```

И посмотрим, как это работает:

```
iex(2)> animal_1 = {:cat, "Tihon"}
{:cat, "Tihon"}
iex(3)> animal_2 = {:dog, "Woof"}
{:dog, "Woof"}
iex(4)> animal_3 = {:rat, "Bet"}
{:rat, "Bet"}
iex(5)> alias ControlFlow, as: CF

iex(6)> CF.handle(animal_1, :pet)
pet the cat Tihon
:ok
iex(7)> CF.handle(animal_2, :feed)
feed the dog Woof
:ok
iex(8)> CF.handle(animal_3, :scratch)
** (CaseClauseError) no case clause matching: :scratch
    control_flow.exs:17: ControlFlow.handle/2
    iex:8: (file)
```

Вложенный даже на два уровня код плохо читается. Обычно этого можно избежать. C помощью сопоставления с образцом можно проверить одновременно несколько условий.

Данный пример можно реализовать без вложенного case таким образом:

```elixir
  def handle2(animal, action) do
    case {animal, action} do
      {{:dog, name}, :feed} -> IO.puts("feed the dog #{name}")
      {{:dog, name}, :pet} -> IO.puts("pet the dog #{name}")
      {{:cat, name}, :feed} -> IO.puts("feed the cat #{name}")
      {{:cat, name}, :pet} -> IO.puts("pet the cat #{name}")
      {{:rat, name}, :feed} -> IO.puts("feed the rat #{name}")
      {{:rat, name}, :pet} -> IO.puts("pet the rat #{name}")
    end
  end
```

Этот код работает точно так же:

```
iex(8)> CF.handle(animal_3, :feed)
feed the rat Bet
:ok
iex(9)> CF.handle(animal_2, :pet)
pet the dog Woof
:ok
```

## Клозы функций (Clause)

Вторая по популярности конструкция (или даже первая, в зависимости от предпочтений разработчика) -- это использование клозов функций.

Этот термин пишется **clause**, произносится **[klôz]** и означает одно из нескольких тел функции.

В Эликсир одна функция может иметь несколько тел -- несколько разных блоков кода. В зависимости от входящих аргументов выполняется только один из этих блоков.

```elixir
  def handle3({:dog, name}, :feed), do: IO.puts("feed the dog #{name}")
  def handle3({:dog, name}, :pet), do: IO.puts("pet the dog #{name}")
  def handle3({:cat, name}, :feed), do: IO.puts("feed the cat #{name}")
  def handle3({:cat, name}, :pet), do: IO.puts("pet the cat #{name}")
```

Шаблоны описываются прямо в аргументах функции, отдельно для каждого тела. Принцип такой же, как и с конструкцией **case** -- шаблоны проверяются по очереди на совпадение с входящими аргументами функции. Первый совпавший шаблон вызывает соответствующий блок кода и останавливает дальшейший перебор. Если ни один шаблон не совпал, то генерируется исключение.

```
iex(13)> CF.handle3(animal_1, :pet)
pet the cat Tihon
:ok
iex(14)> CF.handle3(animal_2, :pet)
pet the dog Woof
:ok
iex(15)> CF.handle3(animal_3, :pet)
** (FunctionClauseError) no function clause matching in ControlFlow.handle3/2
```

Как и в случае с **case**, здесь тоже важна очередность шаблонов. Типичная ошибка -- расположить более общий шаблон выше, чем более специфичный шаблон:

```elixir
  def handle3({animal, name}, action) do
    IO.puts("do '#{action}' with the #{animal} #{name}")
  end
  def handle3({:dog, name}, :feed), do: IO.puts("feed the dog #{name}")
  def handle3({:dog, name}, :pet), do: IO.puts("pet the dog #{name}")
  def handle3({:cat, name}, :feed), do: IO.puts("feed the cat #{name}")
  def handle3({:cat, name}, :pet), do: IO.puts("pet the cat #{name}")
```

Шаблон `{animal, name}` совпадает с любым животным, значит, он более общий, чем шаблон `{:cat, name}`. Значит, он должен идти ниже.

Во многих таких случаях компилятор выдаст предупреждение:

```shell
warning: this clause cannot match because a previous clause at line 37 always matches
  control_flow.exs:40
```

Но бывает, что компилятор не замечает проблему.

При правильном порядке шаблонов:
```
  def handle3({:dog, name}, :feed), do: IO.puts("feed the dog #{name}")
  def handle3({:dog, name}, :pet), do: IO.puts("pet the dog #{name}")
  def handle3({:cat, name}, :feed), do: IO.puts("feed the cat #{name}")
  def handle3({:cat, name}, :pet), do: IO.puts("pet the cat #{name}")
  def handle3({animal, name}, action) do
    IO.puts("do '#{action}' with the #{animal} #{name}")
  end
```

У нас получается правильное поведение:

```
iex(18)> CF.handle3(animal_1, :pet)
pet the cat Tihon
:ok
iex(19)> CF.handle3(animal_3, :pet)
do 'pet' with the rat Bet
```

Нередко последним указывают шаблон, который совпадает с любыми валидными данными. Такой шаблон называется "catch all" (поймать всё).

Но я бы не рекомендовал указывать шаблон, который совпадает с невалидными данными:

```
  def handle4(anything, whatever) do
    IO.puts("do '#{inspect(anything)}' with #{inspect(whatever)}")
  end

iex(23)> CF.handle4([1,2,3], %{a: 42})
do '[1, 2, 3]' with %{a: 42}
```

На таких данных лучше получить исключение:

```
iex(25)> CF.handle3([1,2,3], %{a: 42})
** (FunctionClauseError) no function clause matching in ControlFlow.handle3/2
```

## Охранные выражения (Guards)

Теперь вернемся к упомянутым выше охранным выражениям.

Не всегда достаточно шаблона, чтобы проверить все условия для ветвления в коде. Например, шаблоном нельзя проверить попадание числа в определенный диапазон.

Для примера, мы хотим учесть возраст животного:

```elixir
  def handle4(animal) do
    case animal do
      {:dog, name, age} when age <= 2 -> IO.puts("#{name} is a young dog")
      {:dog, name, _} -> IO.puts("#{name} is an adult dog")
      {:cat, name, age} when age > 10 -> IO.puts("#{name} is an old cat")
      {:cat, name, _} -> IO.puts("#{name} is not so old")
    end
  end
```

Охранные выражения могут использоваться и с **case**, и с телами функций:

```elixir
  def handle5({:dog, name, age}) when age <= 2 do
    IO.puts("#{name} is a young dog")
  end
  def handle5({:dog, name, _age}) do
    IO.puts("#{name} is an adult dog")
  end
  def handle5({:cat, name, age}) when age > 10 do
    IO.puts("#{name} is an old cat")
  end
  def handle5({:cat, name, _age}) do
    IO.puts("#{name} is not so old")
  end
```

Охранное выражение представляет собой один предикат или цепочку предикатов:

```elixir
  def handle6({:library, rating, books}) when rating > 4 and length(books) > 2 do
    IO.puts("good library")
  end

  def handle6({:library, rating, books}) when rating > 4 or length(books) > 2 do
    IO.puts("not too bad")
  end

  def handle6({:library, _rating, _books}) do
    IO.puts("not recommended")
  end
```

```
iex(17)> library_1 = {:library, 5, ["book1", "book2", "book3"]}
{:library, 5, ["book1", "book2", "book3"]}

iex(18)> library_2 = {:library, 2, ["book1", "book2", "book3"]}
{:library, 2, ["book1", "book2", "book3"]}

iex(19)> library_3 = {:library, 5, ["book1"]}
{:library, 5, ["book1"]}

iex(20)> library_4 = {:library, 3, ["book1"]}
{:library, 3, ["book1"]}

iex(21)> CF.handle6(library_1)
good library
:ok
iex(22)> CF.handle6(library_2)
not too bad
:ok
iex(23)> CF.handle6(library_3)
not too bad
:ok
iex(24)> CF.handle6(library_4)
not recommended
:ok
```

В предикатах можно использовать ограниченный набор функций, описанный в [документации](https://hexdocs.pm/elixir/patterns-and-guards.html#list-of-allowed-functions-and-operators). Некоторые функциональные языки разрешают вызывать любые функции в охранных выражениях. Но Эликсир (и Эрланг) не относятся к таким языкам.

```
  def handle7(a) when handle6(a) > 5 do
    :ok
  end
```

```
error: cannot find or invoke local handle6/1 inside guards. Only macros can be invoked in a guards and they must be defined before their invocation. Called as: handle6(a)
  control_flow.exs:91: ControlFlow.handle7/1
```

Кстати, здесь написано про макросы. В охранных выражениях можно использовать макросы, которые раскрываются в валидные выражения. Как пример приводится `Integer.is_even/1`.

```
  require Integer

  def handle8(num) when Integer.is_even(num) do
    IO.puts("#{num} is even")
  end

  def handle8(num) when Integer.is_odd(num) do
    IO.puts("#{num} is odd")
  end
```

```
iex(28)> CF.handle8(10)
10 is even
:ok
iex(29)> CF.handle8(5)
5 is odd
:ok
```

В стандартной библиотеке таких макросов совсем не много. Зато можно реализовать свои.

Если при вычислении охранного выражения возникает исключение, то оно не приводит к остановке процесса, а приводит к тому, что все выражение вычисляется в false:

```elixir
  def handle9(a, b) when 10 / a > 2 do
    {:clause_1, b}
  end

  def handle9(_a, b) do
    {:clause_2, 10 / b}
  end
```

```
iex(47)> CF.handle9(1, 1)
{:clause_1, 1}
iex(48)> CF.handle9(0, 1)
{:clause_2, 10.0}
iex(49)> CF.handle9(1, 0)
{:clause_1, 0}
iex(50)> CF.handle9(0, 0)
** (ArithmeticError) bad argument in arithmetic expression
    control_flow.exs:112: ControlFlow.handle9/2
    iex:50: (file)
```


Это позволяет писать выражения проще. Вместо:

```elixir
  def handle10(a) when is_map(a) and map_size(a) > 2 do
    IO.puts("a big map")
  end

  def handle10(_a) do
    IO.puts("not a big map")
  end
```

можно сразу писать:

```elixir
  def handle10(a) when map_size(a) > 2 do
    IO.puts("a big map")
  end

  def handle10(_a) do
    IO.puts("not a big map")
  end
```

```
iex(54)> CF.handle10(%{a: 1, b: 2})
not a big map
:ok
iex(55)> CF.handle10(%{a: 1, b: 2, c: 3})
a big map
:ok
iex(56)> CF.handle10(42)
not a big map
:ok
iex(57)> is_map(42)
false
iex(58)> map_size(42)
** (BadMapError) expected a map, got: 42
    :erlang.map_size(42)
    iex:58: (file)
iex(58)> map_size(%{a: 1})
1
```
